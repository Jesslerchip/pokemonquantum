raw `Route102_EventScript_LittleBoy::
	msgbox Route102_Text_ImNotVeryTall, MSGBOX_NPC
	end

Route102_EventScript_RouteSignOldale::
	msgbox Route102_Text_RouteSignOldale, MSGBOX_SIGN
	end

Route102_EventScript_RouteSignPetalburg::
	msgbox Route102_Text_RouteSignPetalburg, MSGBOX_SIGN
	end

Route102_EventScript_Boy::
	msgbox Route102_Text_CatchWholeBunchOfPokemon, MSGBOX_NPC
	end

Route102_EventScript_Calvin::
	trainerbattle_single TRAINER_CALVIN_1, Route102_Text_CalvinIntro, Route102_Text_CalvinDefeated, Route102_EventScript_CalvinRegisterMatchCallAfterBattle
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route102_EventScript_CalvinRematch
	setvar VAR_0x8004, TRAINER_CALVIN_1
	specialvar VAR_RESULT, IsTrainerRegistered
	goto_if_eq VAR_RESULT, FALSE, Route102_EventScript_CalvinTryRegister
	msgbox Route102_Text_CalvinPostBattle, MSGBOX_DEFAULT
	release
	end

Route102_EventScript_CalvinRegisterMatchCallAfterBattle::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	goto_if_set FLAG_HAS_MATCH_CALL, Route102_EventScript_CalvinRegisterMatchCall
	release
	end

Route102_EventScript_CalvinRegisterMatchCall::
	msgbox Route102_Text_CalvinRegisterShort, MSGBOX_DEFAULT
	register_matchcall TRAINER_CALVIN_1
	release
	end

Route102_EventScript_CalvinTryRegister::
	goto_if_set FLAG_HAS_MATCH_CALL, Route102_EventScript_CalvinRegister
	msgbox Route102_Text_CalvinPostBattle, MSGBOX_DEFAULT
	release
	end

Route102_EventScript_CalvinRegister::
	msgbox Route102_Text_CalvinRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_CALVIN_1
	release
	end

Route102_EventScript_CalvinRematch::
	trainerbattle_rematch TRAINER_CALVIN_1, Route102_Text_CalvinRematchIntro, Route102_Text_CalvinRematchDefeated
	msgbox Route102_Text_CalvinRematchPostBattle, MSGBOX_AUTOCLOSE
	end

Route102_EventScript_Rick::
	trainerbattle_single TRAINER_RICK, Route102_Text_RickIntro, Route102_Text_RickDefeated
	msgbox Route102_Text_RickPostBattle, MSGBOX_AUTOCLOSE
	end

Route102_EventScript_Tiana::
	trainerbattle_single TRAINER_TIANA, Route102_Text_TianaIntro, Route102_Text_TianaDefeated
	msgbox Route102_Text_TianaPostBattle, MSGBOX_AUTOCLOSE
	end

Route102_EventScript_Allen::
	trainerbattle_single TRAINER_ALLEN, Route102_Text_AllenIntro, Route102_Text_AllenDefeated
	msgbox Route102_Text_AllenPostBattle, MSGBOX_AUTOCLOSE
	end

Route102_Text_WatchMeCatchPokemon:
	.string "WALLY: {PLAYER}…\n"
	.string "POKéMON hide in tall grass like this,\l"
	.string "don't they?\p"
	.string "Please watch me and see if I can\n"
	.string "catch one properly.\p"
	.string "…Whoa!$"

Route102_Text_WallyIDidIt:
	.string "WALLY: I did it… It's my…\n"
	.string "My POKéMON!$"

Route102_Text_LetsGoBack:
	.string "{PLAYER}, thank you!\n"
	.string "Let's go back to the GYM!$"

Route102_Text_ImNotVeryTall:
	.string "I'm…not very tall, so I sink right\n"
	.string "into tall grass.\p"
	.string "The grass goes up my nose and…\n"
	.string "Fwafwafwafwafwa…\p"
	.string "Fwatchoo!$"

Route102_Text_CatchWholeBunchOfPokemon:
	.string "I'm going to catch a whole bunch of\n"
	.string "POKéMON!$"

Route102_Text_RouteSignOldale:
	.string "ROUTE 102\n"
	.string "{RIGHT_ARROW} OLDALE TOWN$"

Route102_Text_RouteSignPetalburg:
	.string "ROUTE 102\n"
	.string "{LEFT_ARROW} PETALBURG CITY$"
`

mapscripts Route102_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if(!flag(FLAG_HIDE_ROUTE_2_RIVAL)) {
			call(Common_EventScript_SetupRivalGfxId)
		}
	}
}

const NURSE = 4
const BELLA = 7

script Route2_EventScript_Nurse {
    lock
	faceplayer
	msgbox(format("Your Pokémon must be tired.\pHere, I can get them healed right up!"))
	closemessage
	fadescreen(FADE_TO_BLACK)
	playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
	fadescreen(FADE_FROM_BLACK)
	msgbox(format("There! Your Pokémon are ready to go!"))
	applymovement(NURSE, Route2_Movement_Bow)
	waitmovement(0)
	closemessage
	release
}

movement Route2_Movement_Bow {
    nurse_joy_bow
    delay_4
}

script Route2_EventScript_CaveSign {
    lockall
    msgbox(format("Springrock Path"))
    releaseall
}

script Route2_EventScript_Timmy {
	trainerbattle_single(TRAINER_TIMMY, Route2_Text_TimmyIntro, Route2_Text_TimmyDefeat)
	msgbox(Route2_Text_TimmyPostBattle, MSGBOX_AUTOCLOSE)
}

text Route2_Text_TimmyIntro {
    format("Hey, you look like a new Trainer!\pI can totally beat you!")
}

text Route2_Text_TimmyDefeat {
    format("I never stood a chance…")
}

text Route2_Text_TimmyPostBattle {
    format("You were way tougher than I thought you'd be. That's not fair!")
}

script Route2_EventScript_Poppy {
	trainerbattle_single(TRAINER_POPPY, Route2_Text_PoppyIntro, Route2_Text_PoppyDefeat)
	msgbox(Route2_Text_PoppyPostBattle, MSGBOX_AUTOCLOSE)
}

text Route2_Text_PoppyIntro {
    format("Rika is the prettiest region in the world! Wouldn't you agree?")
}

text Route2_Text_PoppyDefeat {
    format("Oh, no! My Pokémon!")
}

text Route2_Text_PoppyPostBattle {
    format("Battling isn't everything. In a Contest, looks, attitude, and showmanship all play a role."
    "\pIt's a much more elegant form of Pokémon competition.")
}

script Route2_EventScript_Richtor {
	trainerbattle_single(TRAINER_RICHTOR, Route2_Text_RichtorIntro, Route2_Text_RichtorDefeat)
	msgbox(Route2_Text_RichtorPostBattle, MSGBOX_AUTOCLOSE)
}

text Route2_Text_RichtorIntro {
    format("“Light pink boots trudged through thick layers of snow as a child wandered, seemingly alone, through the woods.”\p"
    "I'm writing a story about Rika! Let's battle for inspiration!")
}

text Route2_Text_RichtorDefeat {
    format("How inspiring!")
}

text Route2_Text_RichtorPostBattle {
    format("“There was a blizzard whipping her around like a leaf, snow pelting her face and ungloved hands.”\pWhat do you think of that line?")
}

script Route2_EventScript_Artist {
	lock
	faceplayer
	msgbox(format("I like to come out here sometimes to get ideas for paintings. Sometimes, if you stand really still, you can even hear the Pokémon."))
	release
}

script Route2_EventScript_ItemPokeDoll {
	finditem(ITEM_POKE_DOLL)
}

script Route2_EventScript_RouteSign {
	lockall
	msgbox(format("Route 2\p{RIGHT_ARROW}Springrock Path\n{DOWN_ARROW}Taproot Town"))
	releaseall
}

script Route2_EventScript_RivalTrigger1 {
	lockall
	playse(SE_PIN)
	playbgm(MUS_ENCOUNTER_BRENDAN, 0)
	applymovement(BELLA, Route2_Movement_BellaApproachTrigger1)
	waitmovement(0)
	goto(Route2_EventScript_RivalBattle)
}

script Route2_EventScript_RivalTrigger2 {
	lockall
	playse(SE_PIN)
	playbgm(MUS_ENCOUNTER_BRENDAN, 0)
	applymovement(BELLA, Route2_Movement_BellaApproachTrigger2)
	waitmovement(0)
	goto(Route2_EventScript_RivalBattle)
}

script Route2_EventScript_RivalTrigger3 {
	lockall
	playse(SE_PIN)
	playbgm(MUS_ENCOUNTER_BRENDAN, 0)
	applymovement(BELLA, Route2_Movement_BellaApproachTrigger3)
	waitmovement(0)
	goto(Route2_EventScript_RivalBattle)
}

script Route2_EventScript_RivalTrigger4 {
	lockall
	playse(SE_PIN)
	applymovement(BELLA, Common_Movement_ExclamationMark)
	waitmovement(0)
	playbgm(MUS_ENCOUNTER_BRENDAN, 0)
	applymovement(BELLA, Route2_Movement_BellaApproachTrigger4)
	waitmovement(0)
	goto(Route2_EventScript_RivalBattle)
}

script Route2_EventScript_RivalBattle {
	msgbox(format("Bella: Hey, {PLAYER}! I told you I'd beat you here!\pYou seriously took Springrock Path? "
	"I was just yanking your chain!\pMe? I had my mother's Braviary carry me here. I've been waiting on you for a while!\p"
	"I've already caught loads of Pokémon for Prof. Sakura, too. I bet they're all better than yours!\p"
	"Here, let me prove it. I'll crush you in a Pokémon battle!"))
	trainerbattle_no_intro(TRAINER_BELLA_1, Route2_Text_BellaDefeated)
	goto(Route2_EventScript_AfterRivalBattle)
}

text Route2_Text_BellaDefeated {
	format("Huh? You beat me? That's impossible!")
}

script Route2_EventScript_AfterRivalBattle {
	msgbox(format("Bella: You must have gotten lucky, {PLAYER}. My Pokémon are the best, and I know it!\p"
	"Let's raise the stakes. Why don't we see who can get the Spectre City Gym Badge first?\pNo way you beat Polter with your team. You need to train up, {PLAYER}!\p"
	"Oh, right. Everyone here is spooked about some guy lurking around with a really strong Pokémon. They say he artificially closed the door to its heart, or something.\p"
	"Watch out, {PLAYER}. You ought to heal at the Pokémon Center before wandering around Taproot Town too much.\p"
	"Otherwise, those whimpy Pokémon of yours are toast!\pSee you after I've got that first Badge, {PLAYER}!"))
	closemessage
	applymovement(BELLA, Route2_Movement_BellaLeaves)
	waitmovement(0)
	setflag(FLAG_HIDE_ROUTE_2_RIVAL)
	removeobject(BELLA)
	setvar(VAR_ROUTE2_STATE, 1)
	clearflag(FLAG_HIDE_TAPROOT_TOWN_VILDER)
	setvar(VAR_TAPROOT_TOWN_STATE, 0)
	fadedefaultbgm
	completesubquest(QUEST_A_SPOOKY_START, SUB_QUEST_2)
	questmenu(QUEST_MENU_BUFFER_QUEST_NAME, QUEST_A_SPOOKY_START)
	call(QuestUpdated)
	closemessage
	releaseall
}

movement Route2_Movement_BellaLeaves {
	walk_left * 2
	walk_down * 10
}

movement Route2_Movement_BellaApproachTrigger1 {
	walk_right
	walk_down
	walk_in_place_right
}

movement Route2_Movement_BellaApproachTrigger2 {
	walk_right
	walk_in_place_right
}

movement Route2_Movement_BellaApproachTrigger3 {
	walk_right
	walk_up * 2
	walk_in_place_up
}

movement Route2_Movement_BellaApproachTrigger4 {
	walk_up * 2
	walk_in_place_up
}